plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.protobuf'
    id 'kotlin-kapt'
}

android {
    compileSdk 31

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "kim.bifrost.rain.bilibili"
        minSdk 24
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    sourceSets {
        main {
            proto {
                srcDir 'src/main/proto'
                include '*.proto'
            }
            java {
                srcDir 'src/main/java'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // 弹幕库
    implementation 'com.github.ctiao:DanmakuFlameMaster:0.9.25'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.6.0'
    // dkPlayer
    implementation 'xyz.doikki.android.dkplayer:dkplayer-java:3.3.5'
    // 使用ijk player进行解码
    implementation 'xyz.doikki.android.dkplayer:player-ijk:3.3.5'
    // dk player ui
    implementation 'xyz.doikki.android.dkplayer:dkplayer-ui:3.3.5'
    // dk player cache
    implementation 'xyz.doikki.android.dkplayer:videocache:3.3.5'

    // room
    implementation "androidx.room:room-ktx:2.4.1"
    kapt "androidx.room:room-compiler:2.4.1"
    // 滑动刷新布局
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    // protobuf
    implementation "com.google.protobuf:protobuf-java:3.19.3"
    // Paging
    implementation "androidx.paging:paging-runtime-ktx:3.1.0"
    // LifeCycle kt
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    // 自己实现的Retrofit
    implementation project(":RainRetrofit")
    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    // 协程
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
    // Gson
    implementation "com.google.code.gson:gson:2.8.9"
    // OKHttp3
    implementation "com.squareup.okhttp3:okhttp:4.9.3"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.19.3'
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.builtins {
                java {}
                // Add cpp output without any option.
                // DO NOT omit the braces if you want this builtin to be added.
            }
        }
    }
    //生成目录
    generatedFilesBaseDir = "$projectDir/src/generated"
}